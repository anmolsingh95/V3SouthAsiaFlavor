namespace = sa_raj

# The Doctrine of Lapse
sa_raj.2 = {
    type = country_event
    placement = ROOT

    title = raj.2.t
    desc = raj.2.d
    flavor = raj.2.f

    duration = 3

    event_image = {
        video = "asia_hinduism_sikhism"
    }

    on_created_soundeffect = "event:/SFX/UI/Alerts/event_appear"

    icon = "gfx/interface/icons/event_icons/event_skull.dds"

    minor_left_icon = c:BIC
    #minor_right_icon = scope:doctrine_of_lapse_target_country

    immediate = {

        if = {
            # take out mysore first
            limit = {
                exists = c:MYS
                c:MYS = {
                    is_subject_of = ROOT
                    is_player = no
                }
            }
            c:MYS = {
                save_scope_as = doctrine_of_lapse_target_country_1
            }
        } else_if = {
            # prioritize hyderabad next
            limit = {
                exists = c:HYD
                c:HYD = {
                    is_subject_of = ROOT
                    is_player = no
                }
            }
            c:HYD = {
                save_scope_as = doctrine_of_lapse_target_country_1
            }
        } else_if = {
            # prioritize nagpur next
            limit = {
                exists = c:NAG
                c:NAG = {
                    is_subject_of = ROOT
                    is_player = no
                }
            }
            c:NAG = {
                save_scope_as = doctrine_of_lapse_target_country_1
            }
        } else_if = {
            # prioritize bastar next
            limit = {
                exists = c:BAS
                c:BAS = {
                    is_subject_of = ROOT
                    is_player = no
                }
            }
            c:BAS= {
                save_scope_as = doctrine_of_lapse_target_country_1
            }
        } else = {
            random_subject_or_below = {
                limit = {
                    is_subject_of = ROOT
                    is_player = no
                    OR = {
                        has_government_type = gov_princely_state_hindu
                        has_government_type = gov_princely_state_muslim
                    }
                }
                save_scope_as = doctrine_of_lapse_target_country_1
            }
        }
        random_subject_or_below = {
            limit = {
                is_subject_of = ROOT
                is_player = no
                OR = {
                    has_government_type = gov_princely_state_hindu
                    has_government_type = gov_princely_state_muslim
                }
                NOT = { this = scope:doctrine_of_lapse_target_country_1 }
            }
            save_scope_as = doctrine_of_lapse_target_country_2
        }
        random_subject_or_below = {
            limit = {
                is_subject_of = ROOT
                is_player = no
                OR = {
                    has_government_type = gov_princely_state_hindu
                    has_government_type = gov_princely_state_muslim
                }
                NOT = { this = scope:doctrine_of_lapse_target_country_1 }
                NOT = { this = scope:doctrine_of_lapse_target_country_2 }
            }
            save_scope_as = doctrine_of_lapse_target_country_3
        }
    }

    option = {
        name = sa_raj.2.a
        default_option = yes
        ai_chance = {
            base = 100 # AI will always pick this
        }
        scope:doctrine_of_lapse_target_country_1 = {
            every_scope_state = {
                remove_building = building_barracks
                set_state_owner = ROOT
                add_radicals_in_state = {
                    value = very_large_radicals
                }
            }
        }
        create_incident = {
            country = scope:doctrine_of_lapse_target_country_1
            value = 20
        }
    }

    option = {
        name = sa_raj.2.b
        trigger = {
            exists = scope:doctrine_of_lapse_target_country_2
        }
        scope:doctrine_of_lapse_target_country_2 = {
            every_scope_state = {
                remove_building = building_barracks
                set_state_owner = ROOT
                add_radicals_in_state = {
                    value = very_large_radicals
                }
            }
        }
        create_incident = {
            country = scope:doctrine_of_lapse_target_country_2
            value = 20
        }
    }
    
    option = {
        name = sa_raj.2.c
        trigger = {
            exists = scope:doctrine_of_lapse_target_country_3
        }
        scope:doctrine_of_lapse_target_country_3 = {
            every_scope_state = {
                remove_building = building_barracks
                set_state_owner = ROOT
                add_radicals_in_state = {
                    value = very_large_radicals
                }
            }
        }
        create_incident = {
            country = scope:doctrine_of_lapse_target_country_3
            value = 20
        }
    }
}